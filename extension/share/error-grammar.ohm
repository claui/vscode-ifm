Messages {
    MessageGroup = Message*

    Message = lineMessage | roomMessage | roomsMessage | cannotSolveMessage

    cannotSolveHeader = "can't solve game" space "(" reasonSummary ")\n"

    cannotSolveMessage = cannotSolveHeader detailSections

    detailSection = sectionHeader sectionDescription

    detailSections = detailSection*

    fileName = (~"," nonNewline)+

    lineKeyword = "line"

    lineMessage = fileName ", " lineKeyword space lineNumber ": " trailingPredicate

    lineNumber = digit+

    nonNewline = ~"\n" any

    predicate = nonNewline*

    quotedRoomName = "'" roomName "'"

    quotedRoomNames = (" and "? quotedRoomName)*

    reasonSummary = (~")" nonNewline)*

    roomKeyword = "room"

    roomMessage = roomKeyword space quotedRoomName space trailingPredicate

    roomName = (~"' " nonNewline)*

    roomsKeyword = "rooms"

    roomsMessage = roomsKeyword space quotedRoomNames space trailingPredicate

    sectionDescription = (sectionIndent sectionIndent textLine)*

    sectionHeader = sectionIndent sectionTitle ":" "\n"

    sectionIndent = space space space

    sectionTitle = (~":" nonNewline)+

    stringLiteral = "'" (~"'" nonNewline)* "'"

    textLine = textLineContent "\n"

    textLineContent = nonNewline*

    trailingPredicate = predicate "\n"
}
